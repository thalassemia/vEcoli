// Global default params
params {
    experimentId = 'EXPERIMENT_ID'
    config = 'CONFIG_FILE'
}

profiles {
    gcloud {
        // Retry once with more RAM if OOM
        process.memory = { 4.GB * task.attempt }
        process.cpus = 2
        process.executor = 'google-batch'
        process.container = 'IMAGE_NAME'
        process.errorStrategy = {
            // Codes: 137 (out-of-memory)
            ((task.exitStatus == 137)
            && (task.attempt <= process.maxRetries)) ? 'retry' : 'ignore' }
        google.project = 'allen-discovery-center-mcovert'
        google.location = 'us-west1'
        google.batch.spot = true
        docker.enabled = true
        params.projectRoot = '/vivarium-ecoli'
        params.publishDir = "PUBLISH_DIR"
        process.maxRetries = 1
    }
    sherlock {
        process.memory = {
            if ( task.exitStatus in [137, 140] ) {
                4.GB * task.attempt
            } else {
                4.GB
            }
        }
        process.errorStrategy = {
            // Codes: 140 (SLURM job limits), 143 (SLURM preemption)
            // Default value for exitStatus is max integer value, this
            // is a catch-all for errors that leave no exit code
            ((task.exitStatus in [140, 143, Integer.MAX_VALUE])
            && (task.attempt <= process.maxRetries)) ? 'retry' : 'ignore' }
        // Using single core is slightly slower but can have shorter
        // queue times and is less damaging to future job priority
        process.cpus = 1
        process.executor = 'slurm'
        process.queue = 'owners'
        process.time = {
            if ( task.exitStatus == 140 ) {
                2.h * task.attempt
            } else {
                2.h
            }
        }
        process.maxRetries = 3
        params.projectRoot = "${launchDir}"
        params.publishDir = "PUBLISH_DIR"
        // Retry all jobs that fail to submit (different from fail during runtime)
        executor.submit.retry.reason = '.*'
        executor.retry.delay = '5 min'
        executor.retry.maxDelay = '10 min'
        // Avoid polling scheduler too frequently (can cause job status mixups)
        executor.pollInterval = '5 min'
        executor.queueStatInterval = '2 min'
        executor.submitRateLimit = '20/min'
        executor.exitReadTimeout = '15 min'
        executor.dumpInterval = '6 min'
        executor.queueSize = 64
    }
    standard {
        process.executor = 'local'
        params.projectRoot = "${launchDir}"
        params.publishDir = "PUBLISH_DIR"
    }
}
